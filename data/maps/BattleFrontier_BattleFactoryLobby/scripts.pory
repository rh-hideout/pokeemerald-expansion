raw `
BattleFrontier_BattleFactoryLobby_MapScripts::
	map_script MAP_SCRIPT_ON_FRAME_TABLE, BattleFrontier_BattleFactoryLobby_OnFrame
	map_script MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE, BattleFrontier_BattleFactoryLobby_OnWarp
	.byte 0

BattleFrontier_BattleFactoryLobby_OnWarp:
	map_script_2 VAR_TEMP_1, 0, BattleFrontier_BattleFactoryLobby_EventScript_TurnPlayerNorth
	.2byte 0

BattleFrontier_BattleFactoryLobby_EventScript_TurnPlayerNorth::
	setvar VAR_TEMP_1, 1
	turnobject LOCALID_PLAYER, DIR_NORTH
	end

BattleFrontier_BattleFactoryLobby_OnFrame:
	map_script_2 VAR_TEMP_CHALLENGE_STATUS, 0, BattleFrontier_BattleFactoryLobby_EventScript_GetChallengeStatus
	map_script_2 VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_SAVING, BattleFrontier_BattleFactoryLobby_EventScript_QuitWithoutSaving
	map_script_2 VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_PAUSED, BattleFrontier_BattleFactoryLobby_EventScript_ResumeChallenge
	map_script_2 VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_WON, BattleFrontier_BattleFactoryLobby_EventScript_WonChallenge
	map_script_2 VAR_TEMP_CHALLENGE_STATUS, CHALLENGE_STATUS_LOST, BattleFrontier_BattleFactoryLobby_EventScript_LostChallenge
	.2byte 0

BattleFrontier_BattleFactoryLobby_EventScript_GetChallengeStatus::
	frontier_getstatus
	end

BattleFrontier_BattleFactoryLobby_EventScript_QuitWithoutSaving::
	lockall
	msgbox BattleFrontier_BattleFactoryLobby_Text_DidntSaveBeforeQuitting, MSGBOX_DEFAULT
	closemessage
	factory_setswapped
	factory_set FACTORY_DATA_WIN_STREAK_SWAPS, 0
	factory_set FACTORY_DATA_WIN_STREAK, 0
	factory_set FACTORY_DATA_WIN_STREAK_ACTIVE, FALSE
	frontier_set FRONTIER_DATA_CHALLENGE_STATUS, 0
	setvar VAR_TEMP_CHALLENGE_STATUS, 255
	releaseall
	end

BattleFrontier_BattleFactoryLobby_EventScript_WonChallenge::
	lockall
	frontier_isbrain
	goto_if_eq VAR_RESULT, TRUE, BattleFrontier_BattleFactoryLobby_EventScript_DefeatedFactoryHead
	msgbox BattleFrontier_BattleFactoryLobby_Text_CongratsSevenWins, MSGBOX_DEFAULT
	waitmessage
	goto BattleFrontier_BattleFactoryLobby_EventScript_GiveBattlePoints

BattleFrontier_BattleFactoryLobby_EventScript_DefeatedFactoryHead::
	msgbox BattleFrontier_BattleFactoryLobby_Text_CongratsForDefeatingHead, MSGBOX_DEFAULT
	waitmessage

BattleFrontier_BattleFactoryLobby_EventScript_FactoryRewardResumeScript::
	msgbox BattleFrontier_BattleFactoryLobby_Text_AwardBattlePoints, MSGBOX_DEFAULT
	frontier_givepoints
	msgbox BattleFrontier_Text_ObtainedXBattlePoints, MSGBOX_GETPOINTS
	message BattleFrontier_BattleFactoryLobby_Text_ExchangeMonsAndSave
	waitmessage
	frontier_checkairshow
	special LoadPlayerParty
	factory_giverewardmon

BattleFrontier_BattleFactoryLobby_EventScript_FactoryRewardSaveAndExitScript::
	callnative UpdateFollowingPokemon
	factory_save 0
	playse SE_SAVE
	waitse
	goto BattleFrontier_BattleFactoryLobby_EventScript_AskRecordBattle
	end

BattleFrontier_BattleFactoryLobby_EventScript_LostChallenge::
	lockall
	message BattleFrontier_BattleFactoryLobby_Text_ReturnMonsSaveResults
	waitmessage
	frontier_checkairshow
	special LoadPlayerParty
	callnative UpdateFollowingPokemon
	factory_set FACTORY_DATA_WIN_STREAK_ACTIVE, FALSE
	factory_save 0
	playse SE_SAVE
	waitse

BattleFrontier_BattleFactoryLobby_EventScript_AskRecordBattle::
	call BattleFrontier_EventScript_GetCantRecordBattle
	goto_if_eq VAR_RESULT, TRUE, BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle
	message BattleFrontier_BattleFactoryLobby_Text_RecordLastMatch
	waitmessage
	multichoicedefault 20, 8, MULTI_YESNO, 1, FALSE
	switch VAR_RESULT
	case 1, BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle
	case 0, BattleFrontier_BattleFactoryLobby_EventScript_RecordBattle
	case MULTI_B_PRESSED, BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle

BattleFrontier_BattleFactoryLobby_EventScript_RecordBattle::
	call BattleFrontier_EventScript_SaveBattle
BattleFrontier_BattleFactoryLobby_EventScript_EndRecordBattle::
	msgbox BattleFrontier_BattleFactoryLobby_Text_LookForwardToNextVisit, MSGBOX_DEFAULT
	closemessage
	setvar VAR_TEMP_CHALLENGE_STATUS, 255
	releaseall
	end

BattleFrontier_BattleFactoryLobby_EventScript_ResumeChallenge::
	lockall
	message BattleFrontier_BattleFactoryLobby_Text_WaitingForYouToResume
	waitmessage
	factory_save CHALLENGE_STATUS_SAVING
	playse SE_SAVE
	waitse
	frontier_set FRONTIER_DATA_PAUSED, FALSE
	setvar VAR_TEMP_CHALLENGE_STATUS, 255
	setvar VAR_0x8006, 2
	goto BattleFrontier_BattleFactoryLobby_EventScript_EnterChallenge

BattleFrontier_BattleFactoryLobby_EventScript_SinglesAttendant::
	lock
	faceplayer
	setvar VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_FACTORY
	setvar VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES
	goto BattleFrontier_BattleFactoryLobby_EventScript_Attendant
	end

BattleFrontier_BattleFactoryLobby_EventScript_DoublesAttendant::
	setvar VAR_FRONTIER_FACILITY, FRONTIER_FACILITY_FACTORY
	setvar VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES
	goto BattleFrontier_BattleFactoryLobby_EventScript_Attendant
	end

BattleFrontier_BattleFactoryLobby_EventScript_Attendant::
	special SavePlayerParty
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES, BattleFrontier_BattleFactoryLobby_EventScript_WelcomeForSingleBattle
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES, BattleFrontier_BattleFactoryLobby_EventScript_WelcomeForDoubleBattle
BattleFrontier_BattleFactoryLobby_EventScript_AskTakeChallenge::
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES, BattleFrontier_BattleFactoryLobby_EventScript_TakeSinglesChallenge
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES, BattleFrontier_BattleFactoryLobby_EventScript_TakeDoublesChallenge
	waitmessage
	multichoice 17, 6, MULTI_CHALLENGEINFO, FALSE
	switch VAR_RESULT
	case 0, BattleFrontier_BattleFactoryLobby_EventScript_TryEnterChallenge
	case 1, BattleFrontier_BattleFactoryLobby_EventScript_ExplainChallenge
	case 2, BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge
	case MULTI_B_PRESSED, BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge

BattleFrontier_BattleFactoryLobby_EventScript_TryEnterChallenge::
	message BattleFrontier_BattleFactoryLobby_Text_WhichLevelMode
	waitmessage
	multichoice 17, 6, MULTI_LEVEL_MODE, FALSE
	switch VAR_RESULT
	case FRONTIER_LVL_TENT, BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge
	case MULTI_B_PRESSED, BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge
	frontier_set FRONTIER_DATA_LVL_MODE, VAR_RESULT
	msgbox BattleFrontier_BattleFactoryLobby_Text_OkayToSaveBeforeChallenge, MSGBOX_YESNO
	switch VAR_RESULT
	case NO, BattleFrontier_BattleFactoryLobby_EventScript_LoadPartyAndCancelChallenge
	case YES, BattleFrontier_BattleFactoryLobby_EventScript_SaveBeforeChallenge
	case MULTI_B_PRESSED, BattleFrontier_BattleFactoryLobby_EventScript_LoadPartyAndCancelChallenge

BattleFrontier_BattleFactoryLobby_EventScript_SaveBeforeChallenge::
	setvar VAR_TEMP_CHALLENGE_STATUS, 0
	factory_init
	frontier_set FRONTIER_DATA_CHALLENGE_STATUS, CHALLENGE_STATUS_SAVING
	factory_set FACTORY_DATA_WIN_STREAK_ACTIVE, TRUE
	frontier_set FRONTIER_DATA_PAUSED, FALSE
	closemessage
	delay 2
	call Common_EventScript_SaveGame
	setvar VAR_TEMP_CHALLENGE_STATUS, 255
	goto_if_eq VAR_RESULT, 0, BattleFrontier_BattleFactoryLobby_EventScript_CancelChallengeSaveFailed
	setvar VAR_0x8006, 0
BattleFrontier_BattleFactoryLobby_EventScript_EnterChallenge::
	special SavePlayerParty
	msgbox BattleFrontier_BattleFactoryLobby_Text_StepThisWay, MSGBOX_DEFAULT
	closemessage
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES, BattleFrontier_BattleFactoryLobby_EventScript_TalkedToSinglesAttendant
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES, BattleFrontier_BattleFactoryLobby_EventScript_TalkedToDoublesAttendant
	applymovement VAR_LAST_TALKED, BattleFrontier_BattleFactoryLobby_Movement_AttendantEnterDoor
	applymovement LOCALID_PLAYER, BattleFrontier_BattleFactoryLobby_Movement_PlayerEnterDoor
	waitmovement 0
	warp MAP_BATTLE_FRONTIER_BATTLE_FACTORY_PRE_BATTLE_ROOM, 8, 13
	setvar VAR_TEMP_CHALLENGE_STATUS, 0
	waitstate
	end

BattleFrontier_BattleFactoryLobby_EventScript_TalkedToSinglesAttendant::
	setvar VAR_LAST_TALKED, LOCALID_FACTORY_ATTENDANT_SINGLES
	return

BattleFrontier_BattleFactoryLobby_EventScript_TalkedToDoublesAttendant::
	setvar VAR_LAST_TALKED, LOCALID_FACTORY_ATTENDANT_DOUBLES
	return

BattleFrontier_BattleFactoryLobby_EventScript_ExplainChallenge::
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_SINGLES, BattleFrontier_BattleFactoryLobby_EventScript_ExplainSinglesChallenge
	call_if_eq VAR_FRONTIER_BATTLE_MODE, FRONTIER_MODE_DOUBLES, BattleFrontier_BattleFactoryLobby_EventScript_ExplainDoublesChallenge
	goto BattleFrontier_BattleFactoryLobby_EventScript_AskTakeChallenge

BattleFrontier_BattleFactoryLobby_EventScript_CancelChallengeSaveFailed::
	frontier_set FRONTIER_DATA_CHALLENGE_STATUS, 0
	goto BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge

BattleFrontier_BattleFactoryLobby_EventScript_LoadPartyAndCancelChallenge::
	special LoadPlayerParty
	callnative UpdateFollowingPokemon
BattleFrontier_BattleFactoryLobby_EventScript_CancelChallenge::
	msgbox BattleFrontier_BattleFactoryLobby_Text_LookForwardToNextVisit, MSGBOX_DEFAULT
	release
	end

BattleFrontier_BattleFactoryLobby_Movement_AttendantEnterDoor:
	walk_up
	walk_up
	walk_up
	set_invisible
	step_end

BattleFrontier_BattleFactoryLobby_Movement_PlayerEnterDoor:
	walk_up
	walk_up
	walk_up
	walk_up
	step_end

BattleFrontier_BattleFactoryLobby_EventScript_WelcomeForSingleBattle::
	msgbox BattleFrontier_BattleFactoryLobby_Text_WelcomeForSingleBattle, MSGBOX_DEFAULT
	return

BattleFrontier_BattleFactoryLobby_EventScript_WelcomeForDoubleBattle::
	msgbox BattleFrontier_BattleFactoryLobby_Text_WelcomeForDoubleBattle, MSGBOX_DEFAULT
	return

BattleFrontier_BattleFactoryLobby_EventScript_TakeSinglesChallenge::
	message BattleFrontier_BattleFactoryLobby_Text_TakeSinglesChallenge
	return

BattleFrontier_BattleFactoryLobby_EventScript_TakeDoublesChallenge::
	message BattleFrontier_BattleFactoryLobby_Text_TakeDoublesChallenge
	return

BattleFrontier_BattleFactoryLobby_EventScript_ExplainSinglesChallenge::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainSinglesChallenge, MSGBOX_DEFAULT
	return

BattleFrontier_BattleFactoryLobby_EventScript_ExplainDoublesChallenge::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainDoublesChallenge, MSGBOX_DEFAULT
	return

BattleFrontier_BattleFactoryLobby_EventScript_ShowSinglesResults::
	lockall
	frontier_results FRONTIER_FACILITY_FACTORY, FRONTIER_MODE_SINGLES
	waitbuttonpress
	special RemoveRecordsWindow
	releaseall
	end

BattleFrontier_BattleFactoryLobby_EventScript_ShowDoublesResults::
	lockall
	frontier_results FRONTIER_FACILITY_FACTORY, FRONTIER_MODE_DOUBLES
	waitbuttonpress
	special RemoveRecordsWindow
	releaseall
	end

BattleFrontier_BattleFactoryLobby_EventScript_Woman::
	msgbox BattleFrontier_BattleFactoryLobby_Text_NeedKnowledgeOfMonsMoves, MSGBOX_NPC
	end

BattleFrontier_BattleFactoryLobby_EventScript_Camper::
	msgbox BattleFrontier_BattleFactoryLobby_Text_SwappedForWeakMon, MSGBOX_NPC
	end

BattleFrontier_BattleFactoryLobby_EventScript_Picnicker::
	lock
	msgbox BattleFrontier_BattleFactoryLobby_Text_NeedToCheckOpponentsMons, MSGBOX_DEFAULT
	release
	end

BattleFrontier_BattleFactoryLobby_EventScript_FatMan::
	msgbox BattleFrontier_BattleFactoryLobby_Text_CantFigureOutStaffHints, MSGBOX_NPC
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesBoard::
	lockall
	msgbox BattleFrontier_BattleFactoryLobby_Text_RulesAreListed, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard::
	message BattleFrontier_BattleFactoryLobby_Text_ReadWhichHeading
	waitmessage
	multichoice 17, 0, MULTI_BATTLE_FACTORY_RULES, FALSE
	switch VAR_RESULT
	case 0, BattleFrontier_BattleFactoryLobby_EventScript_RulesBasics
	case 1, BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapPartner
	case 2, BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapNumber
	case 3, BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapNotes
	case 4, BattleFrontier_BattleFactoryLobby_EventScript_RulesOpenLv
	case 5, BattleFrontier_BattleFactoryLobby_EventScript_ExitRules
	case MULTI_B_PRESSED, BattleFrontier_BattleFactoryLobby_EventScript_ExitRules
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesBasics::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainBasicRules, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapPartner::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainSwapPartnerRules, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapNumber::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainSwapNumberRules, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesSwapNotes::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainSwapNotesRules, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_RulesOpenLv::
	msgbox BattleFrontier_BattleFactoryLobby_Text_ExplainOpenLvRules, MSGBOX_DEFAULT
	goto BattleFrontier_BattleFactoryLobby_EventScript_ReadRulesBoard
	end

BattleFrontier_BattleFactoryLobby_EventScript_ExitRules::
	releaseall
	end

BattleFrontier_BattleFactoryLobby_Text_WelcomeForSingleBattle:
	.string "Where the intelligence of TRAINERS\n"
	.string "is put to the test!\p"
	.string "Welcome to the BATTLE FACTORY!\p"
	.string "I am your guide to the Battle Swap\n"
	.string "Single Tournament.$"

BattleFrontier_BattleFactoryLobby_Text_TakeSinglesChallenge:
	.string "Would you like to take the Battle\n"
	.string "Swap Single challenge?$"

BattleFrontier_BattleFactoryLobby_Text_ExplainSinglesChallenge:
	.string "The Battle Swap Single Tournament\n"
	.string "is a SINGLE BATTLE competition using\l"
	.string "only rental POKéMON.\p"
	.string "First, you will be loaned three\n"
	.string "POKéMON specifically for this event.\p"
	.string "Using the supplied rental POKéMON,\n"
	.string "you must conduct a SINGLE BATTLE.\p"
	.string "If you win, you are permitted to trade\n"
	.string "one of your rental POKéMON.\p"
	.string "Repeat this cycle of battling and\n"
	.string "trading--if you win seven times in\l"
	.string "a row, you will earn Battle Points.\p"
	.string "If you want to interrupt your\n"
	.string "challenge, please save the game.\p"
	.string "If you don't save, you will be\n"
	.string "disqualified from your challenge.$"

BattleFrontier_BattleFactoryLobby_Text_LookForwardToNextVisit:
	.string "We look forward to your next visit.$"

BattleFrontier_BattleFactoryLobby_Text_WhichLevelMode:
	.string "Which level do you wish to challenge?\n"
	.string "Level 50 or Open Level?$"

BattleFrontier_BattleFactoryLobby_Text_OkayToSaveBeforeChallenge:
	.string "Before you begin your challenge,\n"
	.string "I need to save the game. Is that okay?$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_WillHoldMonsForSafekeeping:
	.string "Okay, I will hold your POKéMON for\n"
	.string "safekeeping while you compete.$"

BattleFrontier_BattleFactoryLobby_Text_StepThisWay:
	.string "Please step this way.$"

BattleFrontier_BattleFactoryLobby_Text_ReturnMonsSaveResults:
	.string "Thank you for participating!\p"
	.string "I will return your POKéMON in exchange\n"
	.string "for our rental POKéMON.\p"
	.string "I must also save the event results.\n"
	.string "Please wait.$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_ReturnMons:
	.string "I will return your POKéMON in exchange\n"
	.string "for our rental POKéMON.$"

BattleFrontier_BattleFactoryLobby_Text_CongratsSevenWins:
	.string "Congratulations! You've won seven\n"
	.string "straight Battle Swap matches!$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_AwardBattlePointsForStreak:
	.string "In recognition of your 7-win streak,\n"
	.string "we award you these Battle Point(s).$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_MaxBattlePoints:
	.string "Oh, oh, oh!\p"
	.string "Your Battle Points are maxed.\p"
	.string "Please come back after using\n"
	.string "some Battle Points.$"

BattleFrontier_BattleFactoryLobby_Text_WaitingForYouToResume:
	.string "We've been waiting for you!\p"
	.string "Before we resume your challenge,\n"
	.string "I must save the game.$"

BattleFrontier_BattleFactoryLobby_Text_DidntSaveBeforeQuitting:
	.string "I'm sorry to say this, but you didn't\n"
	.string "save before you quit playing last time.\p"
	.string "As a result, you have been disqualified\n"
	.string "from your challenge.$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_WellReturnMons:
	.string "We'll return your personal POKéMON.$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_ReceivedPrizeItem:
	.string "{PLAYER} received the prize\n"
	.string "{STR_VAR_1}.$"

BattleFrontier_BattleFactoryLobby_Text_WelcomeForDoubleBattle:
	.string "Where the intelligence of TRAINERS\n"
	.string "is put to the test!\p"
	.string "Welcome to the BATTLE FACTORY!\p"
	.string "I am your guide to the Battle Swap\n"
	.string "Double Tournament.$"

BattleFrontier_BattleFactoryLobby_Text_TakeDoublesChallenge:
	.string "Would you like to take the Battle\n"
	.string "Swap Double challenge?$"

BattleFrontier_BattleFactoryLobby_Text_ExplainDoublesChallenge:
	.string "The Battle Swap Double Tournament\n"
	.string "is a DOUBLE BATTLE competition using\l"
	.string "only rental POKéMON.\p"
	.string "First, you will be loaned three\n"
	.string "POKéMON specifically for this event.\p"
	.string "Using the supplied rental POKéMON,\n"
	.string "you must conduct a DOUBLE BATTLE.\p"
	.string "If you win, you are permitted to trade\n"
	.string "one of your rental POKéMON.\p"
	.string "Repeat this cycle of battling and\n"
	.string "trading--if you win seven times in\l"
	.string "a row, you will earn Battle Points.\p"
	.string "If you want to interrupt your\n"
	.string "challenge, please save the game.\p"
	.string "If you don't save before interrupting,\n"
	.string "you will be disqualified.$"

BattleFrontier_BattleFactoryLobby_Text_CongratsForDefeatingHead:
	.string "Congratulations for defeating\n"
	.string "the FACTORY HEAD and winning\l"
	.string "seven matches in a row!$"

BattleFrontier_BattleFactoryLobby_Text_AwardBattlePoints:
	.string "In recognition of your wealth of\n"
	.string "knowledge and keen intelligence,\l"
	.string "we award you these Battle Point(s).$"

BattleFrontier_BattleFactoryLobby_Text_ExchangeMonsAndSave:
	.string "Let me exchange your POKéMON\n"
	.string "for our rental POKéMON.\p"
	.string "I need to save the battle data,\n"
	.string "so please wait.$"

BattleFrontier_BattleFactoryLobby_Text_RecordLastMatch:
	.string "Would you like to record your last\n"
	.string "BATTLE FACTORY match on your\l"
	.string "FRONTIER PASS?$"

BattleFrontier_BattleFactoryLobby_Text_NeedKnowledgeOfMonsMoves:
	.string "Hi!\n"
	.string "You, there!\p"
	.string "Are you thinking that the events here\n"
	.string "are easy since you don't need to have\l"
	.string "a raised team of POKéMON?\p"
	.string "I wouldn't be too sure about winning\n"
	.string "that easily.\p"
	.string "If you don't have thorough knowledge\n"
	.string "about POKéMON and their moves,\l"
	.string "it will be tough to keep winning.$"

BattleFrontier_BattleFactoryLobby_Text_SwappedForWeakMon:
	.string "I swapped for a weak POKéMON…\n"
	.string "I thought it was a good kind to have…\p"
	.string "They wiped the floor with us…$"

BattleFrontier_BattleFactoryLobby_Text_NeedToCheckOpponentsMons:
	.string "Things haven't been going my way\n"
	.string "at all.\p"
	.string "You need to check your opponent's\n"
	.string "POKéMON during battle to see if\l"
	.string "they're any good.$"

BattleFrontier_BattleFactoryLobby_Text_CantFigureOutStaffHints:
	.string "You know how the staff here give you\n"
	.string "a few hints about your next opponent?\p"
	.string "Well, I'm a full-grown man, but I have\n"
	.string "trouble figuring out their hints.$"

@ Unused
BattleFrontier_BattleFactoryLobby_Text_RentalMonsAreVaried:
	.string "Like, I'm really tough, but I get bored\n"
	.string "really easily, so I just kept swapping\l"
	.string "and battling over and over.\p"
	.string "So anyway, when I kept entering\n"
	.string "Battle Swap events, I noticed they\l"
	.string "varied the rental POKéMON.$"

BattleFrontier_BattleFactoryLobby_Text_RulesAreListed:
	.string "The Battle Swap rules are listed.$"

BattleFrontier_BattleFactoryLobby_Text_ReadWhichHeading:
	.string "Which heading do you want to read?$"

BattleFrontier_BattleFactoryLobby_Text_ExplainBasicRules:
	.string "In a Battle Swap event, you may use\n"
	.string "only three POKéMON.\p"
	.string "Whether you are renting or swapping,\n"
	.string "your team may not have two or more\l"
	.string "of the same POKéMON.$"

BattleFrontier_BattleFactoryLobby_Text_ExplainSwapPartnerRules:
	.string "You may swap POKéMON only with\n"
	.string "the TRAINER you have just defeated.\p"
	.string "You may swap for only those POKéMON\n"
	.string "used by the beaten TRAINER.$"

BattleFrontier_BattleFactoryLobby_Text_ExplainSwapNumberRules:
	.string "After every battle you win, you may\n"
	.string "swap for one of your defeated\l"
	.string "opponent's POKéMON.\p"
	.string "You will not be able to swap POKéMON\n"
	.string "with the seventh TRAINER in the event.$"

BattleFrontier_BattleFactoryLobby_Text_ExplainSwapNotesRules:
	.string "There are two key points to be aware\n"
	.string "of when swapping POKéMON.\p"
	.string "First, when swapping, you can't check\n"
	.string "the stats of the POKéMON you are\l"
	.string "about to receive.\p"
	.string "Second, the POKéMON on your team\n"
	.string "are lined up in sequence, depending on\l"
	.string "the order in which you rented them.\p"
	.string "This sequence remains unchanged\n"
	.string "even when swaps are made.$"

BattleFrontier_BattleFactoryLobby_Text_ExplainOpenLvRules:
	.string "In the Open Level, the rental POKéMON\n"
	.string "and the opposing TRAINERS' POKéMON\l"
	.string "are all Level 100.$"
`

script BattleFrontier_BattleFactoryLobby_Noland {
    faceplayer
    msgbox(format("Hey, {PLAYER}. Can't handle the heat? Or just fancy a new challenge? Let me know and I'll adjust some things for ya."))
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandOptions)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandOptions {
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "GIMMICK OPTIONS", "MUSIC OPTIONS", "BACKGROUND OPTIONS", "CANCEL")
	switch(var(VAR_RESULT)) {
		case 0: #
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandGimmickOptions)
        case 1:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        case 2:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandBackgroundOptions)
		case 3:
		    releaseall
        default:
			releaseall
	}
	releaseall
}

// TODO tomorrow - add another set of options here for music choices, by setting VAR_FRONTIER_BGM
// TODO another time - select rental pool options here
// TODO here - set other rules (trick room, random weather, random terrain, inverse battles)
script BattleFrontier_BattleFactoryLobby_EventScript_NolandGimmickOptions {
    msgbox(format("Interested in spicing up your battles?"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "TERASTALLISATION", "MEGA EVOLUTION", "Z-MOVES", "CANCEL")
	switch(var(VAR_RESULT)) {
		case 0: #
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseTeraMode)
        case 1:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseMegaMode)
        case 2:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseZMoveMode)
		case 3:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
	}
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseTeraMode {
    if(flag(FLAG_BATTLE_FACTORY_ALLOW_TERASTALLISATION) == TRUE) {
        msgbox(format("TERASTALLISATION is currently ENABLED."))
    }
    else {
        msgbox(format("TERASTALLISATION is currently DISABLED."))
	}
    msgbox(format("Would you like to change TERASTALLISATION mode?"))
    dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "ENABLE", "DISABLE")
	switch(var(VAR_RESULT)) {
		case 0:
			msgbox(format("You enabled TERASTALLISATION! POKéMON with the TERA BLAST move will also be added to the selection pool."))
			setflag(FLAG_BATTLE_FACTORY_ALLOW_TERASTALLISATION)
		case 1:
			msgbox(format("You disabled TERASTALLISATION!"))
            clearflag(FLAG_BATTLE_FACTORY_ALLOW_TERASTALLISATION)
	}
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseMegaMode {
    if(flag(FLAG_BATTLE_FACTORY_ALLOW_MEGA_EVOLUTION) == TRUE) {
       msgbox(format("MEGA EVOLUTION is currently ENABLED."))
    }
    else {
       msgbox(format("MEGA EVOLUTION is currently DISABLED."))
    }
    msgbox(format("Would you like to change MEGA EVOLUTION mode?"))
    dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "ENABLE", "DISABLE")
    switch(var(VAR_RESULT)) {
    case 0:
        msgbox(format("You enabled MEGA EVOLUTION! POKéMON with the ability to MEGA EVOLVE will be added to the selection pool."))
        setflag(FLAG_BATTLE_FACTORY_ALLOW_MEGA_EVOLUTION)
    case 1:
        msgbox(format("You disabled MEGA EVOLUTION!"))
        clearflag(FLAG_BATTLE_FACTORY_ALLOW_MEGA_EVOLUTION)
    }
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandChooseZMoveMode {
    if(flag(FLAG_BATTLE_FACTORY_ALLOW_Z_MOVES) == TRUE) {
       msgbox(format("Z-MOVES are currently ENABLED."))
    }
    else {
       msgbox(format("Z-MOVES are currently DISABLED."))
    }
    msgbox(format("Would you like to change Z-MOVE mode?"))
    dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE, "ENABLE", "DISABLE")
    switch(var(VAR_RESULT)) {
    case 0:
        msgbox(format("You enabled Z-MOVES! POKéMON with the ability to use Z-MOVES will be added to the selection pool."))
        setflag(FLAG_BATTLE_FACTORY_ALLOW_Z_MOVES)
    case 1:
        msgbox(format("You disabled Z-MOVES!"))
        clearflag(FLAG_BATTLE_FACTORY_ALLOW_Z_MOVES)
    }
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions {
    msgbox(format("I can change the music that plays during your battles!"))
	dynmultichoice(0, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE,
	 "R/S/E MUSIC", "FR/LG MUSIC", "D/P MUSIC", "PLATINUM MUSIC", "HG/SS MUSIC", "BLACK/WHITE MUSIC", "SHUFFLE", "CANCEL")
	switch(var(VAR_RESULT)) {
		case 0:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_RSE)
        case 1:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_FrLg)
        case 2:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_DP)
        case 3:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_Platinum)
        case 4:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_HgSs)
        case 5:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_BlackWhite)
        case 6:
            setflag(FLAG_SHUFFLE_FRONTIER_BGM)
            playse(SE_SUCCESS)
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
		case 7:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
	}
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_RSE {
    msgbox(format("Select battle music from Ruby, Sapphire and Emerald:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	 "VS. TRAINER", "VS. FRONTIER BRAIN", "VS. MEW", "VS. WILD POKEMON", "VS. TEAM AQUA & MAGMA",
     "VS. GYM LEADER", "VS. CHAMPION", "VS. REGI", "VS. KYOGRE & GROUDON", "VS. RIVAL",
     "VS. ELITE FOUR", "VS. AQUA & MAGMA LEADER", "BACK")
	switch(var(VAR_RESULT)) {
		case 0:
            setvar(VAR_FRONTIER_BGM, 0)
        case 1:
            setvar(VAR_FRONTIER_BGM, 1)
        case 2:
            setvar(VAR_FRONTIER_BGM, 2)
        case 3:
            setvar(VAR_FRONTIER_BGM, 3)
        case 4:
            setvar(VAR_FRONTIER_BGM, 4)
        case 5:
            setvar(VAR_FRONTIER_BGM, 5)
        case 6:
            setvar(VAR_FRONTIER_BGM, 6)
        case 7:
            setvar(VAR_FRONTIER_BGM, 7)
        case 8:
            setvar(VAR_FRONTIER_BGM, 8)
        case 9:
            setvar(VAR_FRONTIER_BGM, 9)
        case 10:
            setvar(VAR_FRONTIER_BGM, 10)
        case 11:
            setvar(VAR_FRONTIER_BGM, 11)
		case 12:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
	}
	clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_FrLg {
    msgbox(format("Select battle music from FireRed and LeafGreen:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	      "VS. GYM LEADER", "VS. TRAINER",
          "VS. WILD POKEMON", "VS. CHAMPION", "VS. DEOXYS",
          "VS. MEWTWO", "VS. LEGEND", "BACK")
	switch(var(VAR_RESULT)) {
		case 0:
            setvar(VAR_FRONTIER_BGM, 12)
        case 1:
            setvar(VAR_FRONTIER_BGM, 13)
        case 2:
            setvar(VAR_FRONTIER_BGM, 14)
        case 3:
            setvar(VAR_FRONTIER_BGM, 15)
        case 4:
            setvar(VAR_FRONTIER_BGM, 16)
        case 5:
            setvar(VAR_FRONTIER_BGM, 17)
        case 6:
            setvar(VAR_FRONTIER_BGM, 18)
		case 7:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
	}
    clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_DP {
    msgbox(format("Select battle music from Diamond and Pearl:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	      "VS. WILD POKEMON", "VS. GYM LEADER", "VS. UXIE/ASELF/MESPRIT",
	      "VS. TRAINER", "VS. TEAM GALACTIC BOSS", "VS DIALGA/PALKIA",
	      "VS. CHAMPION", "VS. TEAM GALACTIC", "VS. RIVAL", "VS. ARCEUS",
	      "VS. LEGEND", "VS. GALACTIC COMMANDER", "VS. ELITE FOUR", "BACK")
	switch(var(VAR_RESULT)) {
		case 0:
            setvar(VAR_FRONTIER_BGM, 19)
        case 1:
            setvar(VAR_FRONTIER_BGM, 20)
        case 2:
            setvar(VAR_FRONTIER_BGM, 21)
        case 3:
            setvar(VAR_FRONTIER_BGM, 22)
        case 4:
            setvar(VAR_FRONTIER_BGM, 23)
        case 5:
            setvar(VAR_FRONTIER_BGM, 24)
        case 6:
            setvar(VAR_FRONTIER_BGM, 25)
        case 7:
            setvar(VAR_FRONTIER_BGM, 26)
        case 8:
            setvar(VAR_FRONTIER_BGM, 27)
        case 9:
            setvar(VAR_FRONTIER_BGM, 28)
        case 10:
            setvar(VAR_FRONTIER_BGM, 29)
        case 11:
            setvar(VAR_FRONTIER_BGM, 30)
        case 12:
            setvar(VAR_FRONTIER_BGM, 31)
		case 13:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
	}
    clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_Platinum {
    msgbox(format("Select battle music from Platinum:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	      "VS. GIRATINA", "VS. FRONTIER BRAIN", "VS. REGI", "BACK")
	switch(var(VAR_RESULT)) {
		case 0:
            setvar(VAR_FRONTIER_BGM, 32)
        case 1:
            setvar(VAR_FRONTIER_BGM, 33)
        case 2:
            setvar(VAR_FRONTIER_BGM, 34)
		case 3:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
	}
    clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_HgSs {
    msgbox(format("Select battle music from HeartGold and SoulSilver:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	      "VS. WILD POKEMON", "VS. TRAINER", "VS. GYM LEADER",
          "VS. RIVAL", "VS. TEAM ROCKET", "VS. SUICUNE",
          "VS. ENTEI", "VS. RAIKOU", "VS. CHAMPION", "VS. WILD POKEMON KANTO",
          "VS. TRAINER KANTO", "VS. GYM LEADER KANTO", "VS. HO-OH", "VS. LUGIA",
          "VS. FRONTIER BRAIN", "VS. KYOGRE/GROUDON", "VS. ARCEUS", "BACK")
    switch(var(VAR_RESULT)) {
        case 0:
            setvar(VAR_FRONTIER_BGM, 35)
        case 1:
            setvar(VAR_FRONTIER_BGM, 36)
        case 2:
            setvar(VAR_FRONTIER_BGM, 37)
        case 3:
            setvar(VAR_FRONTIER_BGM, 38)
        case 4:
            setvar(VAR_FRONTIER_BGM, 39)
        case 5:
            setvar(VAR_FRONTIER_BGM, 40)
        case 6:
            setvar(VAR_FRONTIER_BGM, 41)
        case 7:
            setvar(VAR_FRONTIER_BGM, 42)
        case 8:
            setvar(VAR_FRONTIER_BGM, 43)
        case 9:
            setvar(VAR_FRONTIER_BGM, 44)
        case 10:
            setvar(VAR_FRONTIER_BGM, 45)
        case 11:
            setvar(VAR_FRONTIER_BGM, 46)
        case 12:
            setvar(VAR_FRONTIER_BGM, 47)
        case 13:
            setvar(VAR_FRONTIER_BGM, 48)
        case 14:
            setvar(VAR_FRONTIER_BGM, 49)
        case 15:
            setvar(VAR_FRONTIER_BGM, 50)
        case 16:
            setvar(VAR_FRONTIER_BGM, 51)
        case 17:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
    }
    clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions_BlackWhite {
    msgbox(format("Select battle music from Black and White:"))
	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
	      "VS. RIVAL", "VS. WILD POKEMON", "VS. TRAINER", "VS. TEAM PLASMA",
	       "VS. GYM LEADER", "VS. WINNING THEME", "VS. N (1)", "VS. IN A PINCH THEME",
	       "VS. ELITE FOUR", "VS. RESHIRAM/ZEKROM", "VS. N (2)", "VS. GHETSIS",
	       "VS. SUBWAY TRAINER", "VS. LEGEND", "VS. CYNTHIA", "VS. STRONG WILD POKEMON",
	       "VS. KYUREM", "VS. CHAMPION", "VS. WCS CHAMPION", "BACK")
    switch(var(VAR_RESULT)) {
        case 0:
            setvar(VAR_FRONTIER_BGM, 52)
        case 1:
            setvar(VAR_FRONTIER_BGM, 53)
        case 2:
            setvar(VAR_FRONTIER_BGM, 54)
        case 3:
            setvar(VAR_FRONTIER_BGM, 55)
        case 4:
            setvar(VAR_FRONTIER_BGM, 56)
        case 5:
            setvar(VAR_FRONTIER_BGM, 57)
        case 6:
            setvar(VAR_FRONTIER_BGM, 58)
        case 7:
            setvar(VAR_FRONTIER_BGM, 59)
        case 8:
            setvar(VAR_FRONTIER_BGM, 60)
        case 9:
            setvar(VAR_FRONTIER_BGM, 61)
        case 10:
            setvar(VAR_FRONTIER_BGM, 62)
        case 11:
            setvar(VAR_FRONTIER_BGM, 63)
        case 12:
            setvar(VAR_FRONTIER_BGM, 64)
        case 13:
            setvar(VAR_FRONTIER_BGM, 65)
        case 14:
            setvar(VAR_FRONTIER_BGM, 66)
        case 15:
            setvar(VAR_FRONTIER_BGM, 67)
        case 16:
            setvar(VAR_FRONTIER_BGM, 68)
        case 17:
            setvar(VAR_FRONTIER_BGM, 69)
        case 18:
            setvar(VAR_FRONTIER_BGM, 70)
        case 19:
            setvar(VAR_FRONTIER_BGM, 71)
        case 20:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
        default:
            goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandMusicOptions)
    }
    clearflag(FLAG_SHUFFLE_FRONTIER_BGM)
    playse(SE_SUCCESS)
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandBackgroundOptions {
    msgbox(format("Fancy a new lick of paint on your battles?"))
    	dynmultichoice(0, 0, FALSE, 6, 0, DYN_MULTICHOICE_CB_NONE,
    	 "DEFAULT", "SHUFFLE", "GRASS", "LONG GRASS", "SAND", "UNDERWATER", "WATER",
         "POND", "MOUNTAIN", "CAVE", "BUILDING", "PLAIN",
         "GYM", "GYM LEADER", "TEAM MAGMA", "TEAM AQUA",
         "ELITE FOUR SIDNEY", "ELITE FOUR PHOEBE", "ELITE FOUR GLACIA",
         "ELITE FOUR DRAKE", "CHAMPION", "GROUDON", "KYOGRE", "RAYQUAZA", "CANCEL")
    	switch(var(VAR_RESULT)) {
    		case 0:
                setvar(VAR_FRONTIER_ENVIRONMENT, 0)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 1:
                setflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 2:
                setvar(VAR_FRONTIER_ENVIRONMENT, 1)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 3:
                setvar(VAR_FRONTIER_ENVIRONMENT, 2)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 4:
                setvar(VAR_FRONTIER_ENVIRONMENT, 3)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 5:
                setvar(VAR_FRONTIER_ENVIRONMENT, 4)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 6:
                setvar(VAR_FRONTIER_ENVIRONMENT, 5)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 7:
                setvar(VAR_FRONTIER_ENVIRONMENT, 6)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 8:
                setvar(VAR_FRONTIER_ENVIRONMENT, 7)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 9:
                setvar(VAR_FRONTIER_ENVIRONMENT, 8)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 10:
                setvar(VAR_FRONTIER_ENVIRONMENT, 9)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 11:
                setvar(VAR_FRONTIER_ENVIRONMENT, 10)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 12:
                setvar(VAR_FRONTIER_ENVIRONMENT, 11)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 13:
                setvar(VAR_FRONTIER_ENVIRONMENT, 12)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 14:
                setvar(VAR_FRONTIER_ENVIRONMENT, 13)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 15:
                setvar(VAR_FRONTIER_ENVIRONMENT, 14)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 16:
                setvar(VAR_FRONTIER_ENVIRONMENT, 15)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 17:
                setvar(VAR_FRONTIER_ENVIRONMENT, 16)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 18:
                setvar(VAR_FRONTIER_ENVIRONMENT, 17)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 19:
                setvar(VAR_FRONTIER_ENVIRONMENT, 18)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 20:
                setvar(VAR_FRONTIER_ENVIRONMENT, 19)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 21:
                setvar(VAR_FRONTIER_ENVIRONMENT, 20)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 22:
                setvar(VAR_FRONTIER_ENVIRONMENT, 21)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
            case 23:
                setvar(VAR_FRONTIER_ENVIRONMENT, 22)
                clearflag(FLAG_SHUFFLE_FRONTIER_ENVIRONMENT)
    		case 24:
                goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
            default:
                goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
    	}
        playse(SE_SUCCESS)
        goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeBackgroundSuccess)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeMusicSuccess {
	playse(SE_SUCCESS)
	msgbox(format("All done! Your music selection has been changed."))
	waitse
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandChangeBackgroundSuccess {
	playse(SE_SUCCESS)
	msgbox(format("All done! Your background has been changed."))
	waitse
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse)
}

script BattleFrontier_BattleFactoryLobby_EventScript_NolandAnythingElse {
    msgbox(format("Is there anything else I can help you with?"))
    goto(BattleFrontier_BattleFactoryLobby_EventScript_NolandOptions)
}

script BattleFrontier_BattleFactoryLobby_EventScript_GiveBattlePoints {
    msgbox(format("As a reward for your display of skill, you will be able to select one of the POKéMON in your party to keep
                   forever, for use in other BATTLE FRONTIER facilities."))
    factory_selectrewardmon
}