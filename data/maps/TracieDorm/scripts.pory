mapscripts TracieDorm_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
            VAR_PLAYERDORM_STATE, 3: TracieDorm_Enter
    ]
}

script TracieDorm_Enter {
    clearflag(FLAG_HIDE_TRACIE)
    lockall
    applymovement(OBJ_EVENT_ID_PLAYER, moves(walk_up*3))
    waitmovement(0)
    speakername(Tracie)
    msgbox(format("Hmm… Where did I put it…?\nLet's see…\pOh! Found it!\nMy {COLOR BLUE}Quest Log{COLOR DARK_GRAY}!"))
    closemessage
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("Tracie found her {COLOR BLUE}Quest Log{COLOR DARK_GRAY}!"))
    waitfanfare
    closemessage
    applymovement(LOCALID_TRACIE, moves(walk_left, walk_down))
    waitmovement(0)
    speakername(Tracie)
    msgbox(format("Here {PLAYER}, I actually have a second one.\pI'd like you to have it!"))
    closemessage
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox(format("{PLAYER} obtained a {COLOR BLUE}Quest Log{COLOR DARK_GRAY} from Tracie!"))
    waitfanfare
    speakername(Tracie)
    msgbox(format("You can keep track of anything you've been asked to do!\pJust press {START_BUTTON} and select {COLOR BLUE}Quest Log {COLOR DARK_GRAY}\nto view your requests!\pYou can press {R_BUTTON} to filter them too.\pWhy don't we test it out and add one for orientation!"))
    closemessage
    setflag (FLAG_SYS_QUEST_MENU_GET) 
    questmenu(QUEST_MENU_SET_ACTIVE, QUEST_ORIENTATION)
    playse(SE_DEX_SEARCH)
    msgbox(format("{COLOR BLUE}Senior Year{COLOR DARK_GRAY} has been added to your {COLOR BLUE}Quest Log{COLOR DARK_GRAY}."))
    waitse
    openquestmenu
    speakername(Tracie)
    msgbox(format("Easy! Right?\pAnyways, we can't lag too far behind Rotcart.\pOrientation is starting soon, after all.\p Lead the way!\nI'll follow."))
    closemessage
    setfollowernpc(LOCALID_TRACIE, FNPC_ALL, TracieDorm_TracieFollowDialogue, 0)
    setflag(FLAG_HIDE_TRACIE)
    setvar(VAR_PLAYERDORM_STATE, 4)
    end
}

script TracieDorm_TracieFollowDialogue {
    faceplayer
    speakername(Tracie)
    msgbox(format("Hehehe… Senior year!\nLet's go! Let's go!"))
    closemessage
}

script TracieDorm_PichuDoll {
    msgbox("It's a life sized Pichu doll.")
    closemessage
}